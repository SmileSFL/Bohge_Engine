//////////////////////////////////////////////////////////////////////////////////////
//
//						The Bohge Engine License (BEL)
//
//	Copyright (c) 2011-2014 Peng Zhao
//
//	Permission is hereby granted, free of charge, to any person obtaining a copy
//	of this software and associated documentation files (the "Software"), to deal
//	in the Software without restriction, including without limitation the rights
//	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//	copies of the Software, and to permit persons to whom the Software is
//	furnished to do so, subject to the following conditions:
//
//	The above copyright notice and this permission notice shall be included in 
//	all copies or substantial portions of the Software. And the logo of 
//	Bohge Engine shall be displayed full screen for more than 3 seconds 
//	when the software is started. Copyright holders are allowed to develop 
//	game edit based on Bohge Engine, The edit must be released under the MIT 
//	open source license if it is going to be published. In no event shall 
//	copyright holders be prohibited from using any code of Bohge Engine 
//	to develop any other analogous game engines.
//
//	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//
//////////////////////////////////////////////////////////////////////////////////////



#include "LinerDepth.glsl"
#include "Utility.glsl"


uniform sampler2D BaseTexture;
uniform sampler2D TextureDepth;

uniform lowp vec3 vAmbient_Light; //环境光强度乘环境光颜色
uniform lowp vec3 Specular_Light; //高光强度乘灯光颜色
uniform lowp vec3 Light;
uniform mediump float Shininess;
uniform mediump vec3  vLightDirection;
uniform mediump vec2  vInner_DiffInv;//光源的衰减
uniform mediump float fLightRangeInv;
uniform mediump vec4  vAttenuation;


//varying lowp float g_fAttenuation;
//varying highp float g_fDepth;
varying mediump vec2 g_vTexCoord;
varying mediump vec3 g_vLightVec;
varying mediump vec3 g_vViewVec;
varying mediump vec3 g_vNormal;
varying mediump vec4 g_vOutShadowCoord;

void main()
{
	mediump float dis = length( g_vLightVec );
	mediump float disRange = clamp( dis * fLightRangeInv, 0.0, 1.0 );
	lowp float g_fAttenuation = (1.0 - disRange ) / ( vAttenuation.x + disRange * vAttenuation.y + disRange * disRange * vAttenuation.z );
	highp float g_fDepth = GetLineDepthDistance( dis );
	
    lowp vec4 vBaseColor 	= texture2D( BaseTexture, g_vTexCoord );

    // Select the normal in the appropriate space
    mediump vec3 vNormal    = normalize( g_vNormal );

    // Standard Phong lighting
    lowp float fAtten    	= saturate( 1.0 - 0.05 * dot( g_vLightVec, g_vLightVec ) );
    mediump vec3  vLight    = normalize( g_vLightVec );
    mediump vec3  vView     = normalize( g_vViewVec );
    mediump vec3  vHalf     = normalize( vLight + vView );
    lowp float dotLightNormal= dot( vLight, vNormal );

	lowp float attenAll = clamp( 1.0 - ( vInner_DiffInv.x - dot(vLight, -vLightDirection) ) * vInner_DiffInv.y, 0.0, 1.0 );
	
	lowp float lightparam = g_fAttenuation * attenAll * ESMShadowSoptLight( g_fDepth, g_vOutShadowCoord, TextureDepth );
	
	lowp float fDiffuse = saturate( dotLightNormal ) * lightparam;	
	lowp float fSpecular = pow( saturate( dot( vHalf, vNormal ) ) , Shininess ) * lightparam;	
	
	gl_FragColor = vec4( ( vAmbient_Light + fDiffuse * Light ) * vBaseColor.rgb + fSpecular * Specular_Light, vBaseColor.a );
	//gl_FragColor = vec4( attenAll,attenAll,attenAll,1.0);
	//gl_FragColor = vec4( texture2DProj( TextureDepth, g_vOutShadowCoord ).xyz,1.0);
}
