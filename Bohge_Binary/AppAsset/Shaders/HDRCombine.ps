//////////////////////////////////////////////////////////////////////////////////////
//
//						The Bohge Engine License (BEL)
//
//	Copyright (c) 2011-2014 Peng Zhao
//
//	Permission is hereby granted, free of charge, to any person obtaining a copy
//	of this software and associated documentation files (the "Software"), to deal
//	in the Software without restriction, including without limitation the rights
//	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//	copies of the Software, and to permit persons to whom the Software is
//	furnished to do so, subject to the following conditions:
//
//	The above copyright notice and this permission notice shall be included in 
//	all copies or substantial portions of the Software. And the logo of 
//	Bohge Engine shall be displayed full screen for more than 3 seconds 
//	when the software is started. Copyright holders are allowed to develop 
//	game edit based on Bohge Engine, The edit must be released under the MIT 
//	open source license if it is going to be published. In no event shall 
//	copyright holders be prohibited from using any code of Bohge Engine 
//	to develop any other analogous game engines.
//
//	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//
//////////////////////////////////////////////////////////////////////////////////////



precision mediump float;



varying vec2 var_coord;

uniform sampler2D g_SharpTexture;
uniform sampler2D g_CurrentLuminance;
uniform sampler2D g_BloomTexture;

uniform float MiddleGray;
uniform float White;


void main()
{
    lowp vec4 ScreenPixel = texture2D( g_SharpTexture, var_coord );
    lowp vec4 Bloom = texture2D( g_BloomTexture, var_coord );
    
	// Current adaption level of luminance (1x1 texture)
	lowp float AdaptedLum = clamp( texture2D( g_CurrentLuminance, var_coord ).r, 0.1, 1.0 );
    
    // add the bloom render target
    lowp vec4 sample = clamp( ScreenPixel + Bloom, 0.0, 1.0 );

	// RGB -> XYZ conversion
	// http://www.w3.org/Graphics/Color/sRGB
	// The official sRGB to XYZ conversion matrix is (following ITU-R BT.709)
	// 0.4125 0.3576 0.1805
	// 0.2126 0.7152 0.0722
	// 0.0193 0.1192 0.9505	
	const mat3 RGB2XYZ = mat3( 0.5141364, 0.3238786,  0.16036376,
							   0.265068,  0.67023428, 0.06409157,
							   0.0241188, 0.1228178,  0.84442666);				            
	// Make sure none of XYZ can be zero
	// This prevents temp from ever being zero, or Yxy from ever being zero        
	mediump vec3 XYZ = ( RGB2XYZ * sample.rgb ) + vec3( 0.000001, 0.000001, 0.000001 );
	
	// XYZ -> Yxy conversion
	mediump vec3 Yxy;
	Yxy.r = XYZ.g;									// copy luminance Y

	mediump float temp = dot( vec3( 1.0, 1.0, 1.0 ), XYZ.rgb );
	//Yxy.g = XYZ.r / temp;  							// x = X / (X + Y + Z)
	//Yxy.b  = XYZ.g / temp; 							// y = X / (X + Y + Z)	
	Yxy.gb = XYZ.rg / temp;
	
	// (Lp) Map average luminance to the middlegrey zone by scaling pixel luminance
	// raise the value range a bit ...
	mediump float Lp = Yxy.r * MiddleGray / ( AdaptedLum + 0.001 );    

	// (Ld) Scale all luminance within a displayable range of 0 to 1
	Yxy.r = ( Lp  * ( 1.0 + Lp / White ) ) / ( 1.0 + Lp );

	// Yxy -> XYZ conversion
	XYZ.r = Yxy.r * Yxy.g / Yxy.b;               // X = Y * x / y
	XYZ.g = Yxy.r;                                // copy luminance Y
	XYZ.b = Yxy.r * ( 1.0 - Yxy.g - Yxy.b ) / Yxy.b;  // Z = Y * (1-x-y) / y

	// XYZ -> RGB conversion
	// The official XYZ to sRGB conversion matrix is (following ITU-R BT.709)
	//  3.2410 -1.5374 -0.4986
	// -0.9692  1.8760  0.0416
	//  0.0556 -0.2040  1.0570		
	const mat3 XYZ2RGB  = mat3( 2.5651, -1.1665, -0.3986,
							   -1.0217,  1.9777,  0.0439, 
							    0.0753, -0.2543,  1.1892);
#if 0
	// Night Tone mapper: for scotopic images
	// reduces brightness and contrast, desaturates the image, adds a blue shift and reduces visual acuity 
	// 
	// Pseudo: Y * (1.33 * (1+((Y+Z)/X)-1.68)
	mediump float V = XYZ.g *(1.33*(1.0+((XYZ.g+XYZ.b) / XYZ.r)-1.68));
	mediump vec3 NightColor = V * BlueShiftColor.rgb;
	sample.rgb = lerp(NightColor, mul(XYZ2RGB, XYZ), saturate(Yxy.r * BlueShiftParams.r + (1.0 - AdaptedLum.x) * BlueShiftParams.b + BlueShiftParams.g));
#else    
	sample.rgb = (XYZ2RGB * XYZ);	
#endif
		
	gl_FragColor = clamp(vec4(sample.r, sample.g, sample.b, ScreenPixel.a), 0.0, 1.0);
}
