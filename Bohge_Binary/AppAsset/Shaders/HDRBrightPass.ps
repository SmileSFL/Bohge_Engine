//////////////////////////////////////////////////////////////////////////////////////
//
//						The Bohge Engine License (BEL)
//
//	Copyright (c) 2011-2014 Peng Zhao
//
//	Permission is hereby granted, free of charge, to any person obtaining a copy
//	of this software and associated documentation files (the "Software"), to deal
//	in the Software without restriction, including without limitation the rights
//	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//	copies of the Software, and to permit persons to whom the Software is
//	furnished to do so, subject to the following conditions:
//
//	The above copyright notice and this permission notice shall be included in 
//	all copies or substantial portions of the Software. And the logo of 
//	Bohge Engine shall be displayed full screen for more than 3 seconds 
//	when the software is started. Copyright holders are allowed to develop 
//	game edit based on Bohge Engine, The edit must be released under the MIT 
//	open source license if it is going to be published. In no event shall 
//	copyright holders be prohibited from using any code of Bohge Engine 
//	to develop any other analogous game engines.
//
//	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//
//////////////////////////////////////////////////////////////////////////////////////



precision mediump float;


uniform sampler2D g_ScratchTexture;
uniform sampler2D g_CurrentLuminance;
uniform float MiddleGray;
uniform float BrightPassWhite;
uniform float BrightPassThreshold;
uniform float BrightPassOffset;

varying vec2 var_coord;

void main()
{
	// fetch scratch render target
	vec4 Sample = texture2D( g_ScratchTexture, var_coord);
	
    // Current adaption level of luminance (1x1 texture)
	float AdaptedLum = clamp(texture2D( g_CurrentLuminance, var_coord ).r, 0.1, 1.0);
    
	// RGB -> XYZ conversion
	const mat3 RGB2XYZ = mat3(0.5141364, 0.3238786,  0.16036376,
							 0.265068,  0.67023428, 0.06409157,
							 0.0241188, 0.1228178,  0.84442666);	
							 			                    
	vec3 XYZ = RGB2XYZ * Sample.rgb;	
	
	// XYZ -> Yxy conversion
	vec3 Yxy;
	Yxy.r = XYZ.g;                            // copy luminance Y
	
	// prevent division by zero to keep the compiler happy ... strange
	float temp = dot(vec3(1.0,1.0,1.0), XYZ.rgb) + 0.000001; 
	//Yxy.g = XYZ.r / temp; // x = X / (X + Y + Z)
	//Yxy.b  = XYZ.g / temp; // y = X / (X + Y + Z)	
	Yxy.gb = XYZ.rg / temp;
	
    // (Lp) Map average luminance to the middlegrey zone by scaling pixel luminance
    // raise the value range a bit ...
	float Lp = Yxy.r * MiddleGray / (AdaptedLum + 0.001);    

	// (Ld) Scale all luminance within a displayable range of 0 to 1
	// new bright pass white value
	Yxy.r = (Lp  * (1.0 + Lp /(BrightPassWhite))) ;	

	Yxy.r -= BrightPassThreshold;
	
	Yxy.r = max(Yxy.r, 0.0);
    Yxy.r /= (BrightPassOffset + Yxy.r);

	// Yxy -> XYZ conversion
	XYZ.r = Yxy.r * Yxy.g / Yxy. b;               // X = Y * x / y
	XYZ.g = Yxy.r;                                // copy luminance Y
	XYZ.b = Yxy.r * (1.0 - Yxy.g - Yxy.b) / Yxy.b;  // Z = Y * (1-x-y) / y

	// XYZ -> RGB conversion
	const mat3 XYZ2RGB  = mat3(2.5651,-1.1665,-0.3986,
							  -1.0217, 1.9777, 0.0439, 
							   0.0753, -0.2543, 1.1892);
							   
	Sample.rgb = clamp((XYZ2RGB * XYZ), 0.0, 1.0);	    

	gl_FragColor = vec4(Sample.x, Sample.y, Sample.z, 1.0);
}
